---
id: ring
title: Ring
sidebar_label: Ring
---
GraphQLize built on top of [Lacinia](https://lacinia.readthedocs.io/en/latest/), a de-facto library for implementing GraphQL APIs in Clojure.

Getting started with GraphQLize using Ring involves only few steps. Let's dive in.

## Adding Dependencies

Create a new Clojure project using [deps](https://github.com/seancorfield/clj-new) (or [leiningen](https://leiningen.org/)) and add the GraphQLize and other dependencies.

[![Clojars Project](https://img.shields.io/clojars/v/org.graphqlize/graphqlize.svg)](https://clojars.org/org.graphqlize/graphqlize)

```clj {4}
;; deps.edn
{:paths ["src" "resources"]
 :deps  {org.clojure/clojure              {:mvn/version "1.10.1"}
         org.graphqlize/graphqlize        {:mvn/version "0.1.0-alpha7"}
         ;; db connection pool
         hikari-cp                        {:mvn/version "2.10.0"}
         ;; for postgres
         org.postgresql/postgresql        {:mvn/version "42.2.8"}
         ;; for MySQL
         mysql/mysql-connector-java       {:mvn/version "8.0.19"}
         ;; Ring Router
         metosin/reitit             {:mvn/version "0.4.2"}
         ;; Web Server
         ring/ring-jetty-adapter    {:mvn/version "1.7.1"}}}
```

## Configuring DataSource

The next step is configuring the [DataSource](https://docs.oracle.com/javase/7/docs/api/javax/sql/DataSource.html). In this example, we are going to use [Hikari Connection Pool](https://github.com/brettwooldridge/HikariCP) to manage the database connection.

:::note
For brevity, this sample uses `def` to define the states. In a real-world project, you can replace it with [Component](https://github.com/stuartsierra/component), [Mount](https://github.com/tolitius/mount), or [Integrant](https://github.com/weavejester/integrant).
:::


import Tabs from "@theme/Tabs";
import TabItem from "@theme/TabItem";

<Tabs
  defaultValue="postgres"
  values={[
    { label: 'Postgres', value: 'postgres' },
    { label: 'MySQL', value: 'mysql' }
  ]
}>
<TabItem value="postgres">

```clojure
;; src/server.clj
(ns server
  (:require [hikari-cp.core :as hikari]))

(def db-spec (hikari/make-datasource {:adapter           "postgresql"
                                      :database-name     "sakila"
                                      :server-name       "localhost"
                                      :port-number       5432
                                      :maximum-pool-size 1
                                      :username          "postgres"
                                      :password          "postgres"}))
```

</TabItem>
<TabItem value="mysql">

```clojure
;; src/server.clj
(ns server
  (:require [hikari-cp.core :as hikari]))

(def db-spec (hikari/make-datasource {:server-name       "localhost"
                                      :maximum-pool-size 1
                                      :jdbc-url          "jdbc:mysql://localhost:3306/sakila"
                                      :driver-class-name "com.mysql.cj.jdbc.MysqlDataSource"
                                      :username          "root"
                                      :password          "mysql123"}))
```

</TabItem>
</Tabs>

:::note
Make sure you are changing the above values to refer your database connection. The above example assumes that you are using the _sakila_ database created from this [JOOQ's example repository](https://github.com/jOOQ/jOOQ/tree/master/jOOQ-examples/Sakila).  
:::

## Creating Lacinia Schema

Then create a lacinia schema from the data source using GraphQLize.  


```clojure {3,7}
(ns server
  (:require ; ...
            [graphqlize.lacinia.core :as l]))

(def db-spec ...)

(def lacinia-schema (l/schema db-spec))
```

## Adding GraphQL Endpoint

The final step is adding a Ring route `/graphql` to expose the GraphQL API. 

```clojure {12,15,17}
(ns server
  (:require ;...
            [ring.adapter.jetty :as jetty]
            [ring.util.request :as request]
            [reitit.ring :as ring]
            [com.walmartlabs.lacinia :as lacinia]
            [clojure.data.json :as json]))
; ...

(def lacinia-schema ...)

(defn graphql-handler [request]
  (let [grapql-request (json/read-str (request/body-string request) :key-fn keyword)
        {:keys [query variables]} grapql-request
        result (lacinia/execute lacinia-schema query variables nil)]
    {:status  200
     :body    (json/write-str result)
     :headers {"Content-Type" "application/json"}}))

(def app
  (ring/ring-handler (ring/router ["/graphql" {:post handler}])))

(defn start-server []
  (jetty/run-jetty app {:join? false
                        :port  8080}))

(defn -main []
  (start-server))
```

The `graphql-handler` function does the following

* It de-serializes the GraphQL request body from the Ring request.
* Then using the `lacinia-schema` created in the previous step, it executes this GraphQL request by invoking the Lacinia's [execute](https://walmartlabs.github.io/apidocs/lacinia/com.walmartlabs.lacinia.html#var-execute) function.
* Finally, it serializes the `result` of the `execute` function and send it back to the client as a Ring response.

:::note
This sample uses [Reitit](https://github.com/metosin/reitit) without [coercion](https://metosin.github.io/reitit/coercion/coercion.html) for simplicity. An example of the above sample using Reitit's [Spec coercion](https://metosin.github.io/reitit/coercion/clojure_spec_coercion.html) is available in [this file](https://github.com/graphqlize/graphqlize-demo/blob/master/clojure/ring/src/server_with_coercion.clj).
:::

### Test Drive

To do a test drive of this implementation, start the server

```bash
> clj -m server
```

 and hit the endpoint via curl.

```bash
> curl -X POST \
  --data '{"query": "query { actorByActorId(actorId: 1){firstName}}"}' \
  -H "Content-Type: application/json" \
  http://localhost:8080/graphql
```

You'll get a response like below.

```json
{
  "data": {
    "actorByActorId": {
      "firstName": "PENELOPE"
    }
  }
}
```

## GraphQL Playground and Voyager

With the GraphQL endpoint up and running, the next step is introspecting the GraphQL schema and try out some more queries. 

To introspect, we are going to make use of [Voyager](https://github.com/APIs-guru/graphql-voyager), a tool to visualize GraphQL API as an interactive graph. To add Voyager, download this [voyager.html](https://github.com/graphqlize/graphqlize-demo/blob/master/clojure/ring/resources/static/voyager.html) file and put it under the _resources/static_ directory.

Then configure Ring to use this _static_ directory for serving static files.

```clojure
(ns server
  (:require ;...
            [ring.middleware.resource :as resource]
            [ring.middleware.content-type :as content-type]
            [ring.middleware.not-modified :as not-modified]))
; ...
(defn handler ...)

(def app
  (-> (ring/ring-handler (ring/router ["/graphql" {:post handler}]))
      (resource/wrap-resource "static")
      content-type/wrap-content-type
      not-modified/wrap-not-modified))
```

When you restart the server, the Voyager will be available at _http://localhost:8080/voyager.html_. A sample output would look like [this](https://www.graphqlize.org/html/sakila-voyager.html).

Then to interact with the GraphQL API, let's add the [GraphQL Playground](https://github.com/prisma-labs/graphql-playground). Like Voyager, download this [playground.html](https://github.com/graphqlize/graphqlize-demo/blob/master/clojure/ring/resources/static/playground.html) file and put in the _static_ directory. 

This GraphQL playground will be available at _http://localhost:8080/playground.html_ after server restart. 

![](/img/graphql-playground.png)

## Next Steps

Congrats! You are on course to build impressive applications using GraphQLize in less time. To save yourself some more time, do refer [this documentation](/docs/graphql_schema/types-and-fields) to know more about how GraphQLize generates the GraphQL schema and the queries. 

The sample code is available in [this GitHub Repository](https://github.com/graphqlize/graphqlize-demo/tree/master/clojure/ring).

:::note
You can also customize certain default behaviours of GraphQLize in future releases.
:::